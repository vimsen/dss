#!/usr/bin/gnuplot

if (!exists ("ext")) ext='eps'                                                  
if (ext eq 'eps') {                                                             
  print "first: ".ext                                                           
    set term postscript eps enhanced color size 3.5,2.62 linewidth 3              
} else { if (ext eq 'emf') {                                                    
  print "second: ".ext                                                          
    set terminal emf linewidth 3 size 640,480                                     
}}   


set datafile separator ','

list = system('ls plot_data_10_100_*.csv')

do for [file in list] {
  bname = system("basename '".file."' .csv")
  set out bname.'.eps'
  set xlabel "15 minute time period"
  set ylabel "Prosumption (kWh)"
  set key bottom left
#  set yrange [0:20]
  plot file using 1:2 w lp t 'target', \
       '' u 1:3 w lp t 'predicted', \
       '' u 1:4 w lp t 'actual', \
       '' u 1:5 w lp t 'predicted (no forecast)', \
       '' u 1:6 w lp t 'actual (no forecast)'
}

list = system('ls target_errors_*.csv')

set ylabel "Mean deviation from the target (kWh)"

do for [file in list] {
  bname = system("basename '".file."' .csv")
  set out bname.'.eps'
  for_order = bname[15:]
#   set yrange [0:150]
  set key top left
  plot file u 1:2:3 w yerrorbars t 'forecast '.for_order lt 1, \
         '' u 1:2 w lp lt 1 notitle, \
         '' u 1:4:5 w yerrorbars t 'no forecast' lt 2, \
         '' u 1:4 w lp lt 2 notitle
}

set out 'target_errors_comparison.eps'
# set yrange [0:50]
plot for [for_order=10:100:10] 'target_errors_'.for_order.'.csv' using 1:2 w l t 'forecast '.for_order
